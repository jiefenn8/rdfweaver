plugins {
    id 'idea'
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'jacoco'
}

group 'com.github.jiefenn8.rdfweaver'
archivesBaseName = 'rdfweaver-console'
description = 'RDFWeaver console - multi-media to semantic graph mapping application.'

version '0.1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven {
        url  "https://dl.bintray.com/jiefenn8/graphloom"
    }
}

sourceSets {
    integrationTest {
        java{
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

mainClassName = 'com.github.jiefenn8.rdfweaver.EntryPoint'

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

dependencies {
    //Test
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.hamcrest', name: 'java-hamcrest', version: '2.0.0.0'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.11.0'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

    //Core
    implementation group: 'info.picocli', name: 'picocli', version: '4.2.0'
    implementation group: 'com.github.jiefenn8.graphloom', name: 'graphloom-core', version: '0.4.5'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'org.apache.jena', name: 'jena-arq', version: '3.5.0'
    implementation group: 'org.apache.jena', name: 'jena-rdfconnection', version: '3.5.0'

    //Logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.0'

    //Network
    implementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '6.2.2.jre8'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.20'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.2.0'
    implementation group: 'com.oracle.database.jdbc', name: 'ojdbc8', version: '19.3.0.0'

    //Annotation Processor
    annotationProcessor group: 'info.picocli', name: 'picocli-codegen', version: '4.2.0'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task integrationTest(type: Test){
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task integrationTestReport(type :JacocoReport){
    sourceSets sourceSets.main
    executionData integrationTest
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
integrationTest.finalizedBy integrationTestReport

publishing {
    publications {
        RDFWeaver(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'rdfweaver'
        name = project.name
        userOrg = 'jiefenn8'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jiefenn8/rdfweaver.git'

        version {
            name = project.version
            desc = project.description
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }

    publications = ['RDFWeaver']
}
