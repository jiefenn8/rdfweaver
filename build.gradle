plugins {
    id 'idea'
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = 'com.github.jiefenn8.rdfweaver'
archivesBaseName = 'rdfweaver-cli'
description = 'RDFWeaver console - multi-media to semantic graph mapping application.'

version = '0.1.0'

sourceCompatibility = 1.15
targetCompatibility = 1.15

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = '0.8.7'
}

sourceSets {
    integrationTest {
        java{
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

mainClassName = 'com.github.jiefenn8.rdfweaver.EntryPoint'

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

dependencies {
    //Test
    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.7.7'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'

    //Core
    implementation group: 'info.picocli', name: 'picocli', version: '4.6.1'
    implementation group: 'io.github.jiefenn8.graphloom', name: 'graphloom-core', version: '0.5.1'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    implementation group: 'org.apache.jena', name: 'jena-arq', version: '3.5.0'
    implementation group: 'org.apache.jena', name: 'jena-rdfconnection', version: '3.5.0'

    //Logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.1'

    //Network
    implementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '9.2.0.jre15'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.25'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
    implementation group: 'com.oracle.database.jdbc', name: 'ojdbc8', version: '19.3.0.0'

    //Annotation Processor
    annotationProcessor group: 'info.picocli', name: 'picocli-codegen', version: '4.6.1'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
    }
}

task integrationTest(type: Test){
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task integrationTestReport(type :JacocoReport){
    dependsOn integrationTest
    group = 'verification'
    description = 'Generates code coverage report for the integration test task.'
    sourceSets sourceSets.main
    executionData integrationTest
    reports {
        xml.enabled true
        html.enabled false
    }
}

test.finalizedBy jacocoTestReport
integrationTest.finalizedBy integrationTestReport

